buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.3"
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.14"
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.2"
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.google.protobuf"
apply plugin: "java"
apply plugin: "maven"
apply plugin: "net.ltgt.apt"

repositories {
    mavenLocal()
    jcenter()
    google()
}

configurations {
    compileWindows
    compileMacOs
    compileLinux
}

// The repackaging rules are defined in the "shadowJar" task below.
dependencies {
    compile "com.android.tools:r8:1.0.10"
    compile "com.android.tools.build:apkzlib:3.2.0-alpha06"
    compile "com.android.tools.ddms:ddmlib:26.2.0-alpha04"

    shadow "com.android.tools.build:aapt2-proto:0.3.1"
    shadow "com.google.auto.value:auto-value:1.5.2"
    apt "com.google.auto.value:auto-value:1.5.2"
    shadow "com.google.errorprone:error_prone_annotations:2.2.0"
    shadow "com.google.guava:guava:22.0"
    shadow "com.google.protobuf:protobuf-java:3.4.0"
    shadow "com.google.protobuf:protobuf-java-util:3.4.0"

    compileWindows "com.android.tools.build:aapt2:3.2.0-alpha14-4748712:windows"
    compileMacOs "com.android.tools.build:aapt2:3.2.0-alpha14-4748712:osx"
    compileLinux "com.android.tools.build:aapt2:3.2.0-alpha14-4748712:linux"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.4.0"
    }
}

uploadShadow {
    repositories {
        mavenDeployer {
            def localRepo = project.hasProperty('localRepo') ?
                    project.localRepo : "$buildDir/repo"
            repository(url: "file://" + localRepo)
            pom.project {
                groupId 'com.android.tools.build'
                artifactId 'bundletool'
                version project.release_version

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

// Artifact released to Maven.
shadowJar {
    baseName = 'bundletool'
    classifier = ''

    // Package all the Android Gradle plugin dependencies that are compiled from
    // source.
    dependencies {
        include(dependency {
            it.moduleGroup.startsWith('com.android.tools')
        })
    }

    relocate('com.android', 'shadow.bundletool.com.android') {
        // BundleTool classes.
        exclude 'com.android.tools.build.bundletool.**'
        // Bundle protos.
        exclude 'com.android.bundle.**'
        // Aapt protos.
        exclude 'com.android.aapt.**'
    }
}

// Artifact to use as standalone command line tool.
task executableJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    baseName = 'bundletool'
    classifier = 'all'
    from sourceSets.main.output
    from({ zipTree(project.configurations.compileWindows.singleFile) }) { into 'windows/' }
    from({ zipTree(project.configurations.compileMacOs.singleFile) }) { into 'macos/' }
    from({ zipTree(project.configurations.compileLinux.singleFile) }) { into 'linux/' }
    configurations = [
        project.configurations.runtime,
        project.configurations.shadow
    ]
    manifest {
        attributes 'Main-Class': 'com.android.tools.build.bundletool.BundleToolMain'
    }
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}
